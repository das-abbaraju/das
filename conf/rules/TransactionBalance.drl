package com.picsauditing.jpa.entities

import java.math.BigDecimal

rule "Update Amount"
when
	$inv : Invoice(  )
	$amount : BigDecimal(  )
				from accumulate ( InvoiceItem ( invoice == $inv, $value: amount ) from $inv.items,
									init( BigDecimal total = BigDecimal.ZERO; ),
									action( total = total.add($value); ),
									reverse( total = total.subtract($value); ),
									result( total ) )
	Invoice( id == $inv.id, $inv.totalAmount != $amount)
then
	modify ($inv) { setTotalAmount($amount) }
end

rule "Update Amount Applied"
when
	$inv : Invoice( )
	$amount: BigDecimal(  )
				from accumulate ( PaymentAppliedToInvoice ( invoice == $inv, $value: amount ) from $inv.payments,
									init( BigDecimal total = BigDecimal.ZERO; ),
									action( total = total.add($value); ),
									reverse( total = total.subtract($value); ),
									result( total ) )
	Invoice( id == $inv.id, $inv.amountApplied != $amount )
then
	modify ($inv) { setAmountApplied($amount) }
end

rule "Paid"
salience -10
when
	$tr : Transaction( status != TransactionStatus.Void, totalAmount.doubleValue != 0, balance.doubleValue == 0 )
then
	$tr.setStatus(TransactionStatus.Paid);
end

rule "UnPaid"
salience -10
when
	$tr : Transaction( (status != TransactionStatus.Void) &&  (totalAmount.doubleValue == 0 || balance.doubleValue != 0) )
then
	$tr.setStatus(TransactionStatus.Unpaid);
end
